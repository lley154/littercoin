spending validator

struct Datum {
    adaAmount: Int
    lcAmount: Int

    func get_ratio(self) -> Int {
        self.adaAmount / self.lcAmount
    }
}

enum Redeemer {
    AddAda
    Mint
    Burn { 
        pkhBA: ByteArray 
    }
}

const minAda : Int = 2000000
const minAdaVal: Value = Value::lovelace(minAda)

// Define thread token value
const TT_MPH: ByteArray = #a9c693fbb5065379c26517e9ab36a08f3d4ceee556ae7e26b62a401d
const ttMph: MintingPolicyHash = MintingPolicyHash::new(TT_MPH)
const ttAssetclass: AssetClass = AssetClass::new(
        ttMph, 
        "Thread Token Littercoin".encode_utf8()
    )
const ttVal : Value = Value::new(ttAssetclass, 1)

// Define the mph of the littercoin minting policy
const LC_MPH: ByteArray = #4d6fd047c7b8192aac55770dfaf226a17d56ac1cc15dc02cf6fb71f9
const lcMph: MintingPolicyHash = MintingPolicyHash::new(LC_MPH)
const lcAssetClass: AssetClass = AssetClass::new(
        lcMph, 
        "Littercoin".encode_utf8()
    )

// Define the pkh of the owner
const OWNER_PKH: ByteArray = #b9abcf6867519e28042048aa11207214a52e6d5d3288b752d1c27682 
const ownerPkh: PubKeyHash = PubKeyHash::new(OWNER_PKH)

// Define the merchant token
const MERCHANT_MPH: ByteArray = #b63f671dcfbb0daaaf081f3b339243c7a533637d9755e5387a232a67
const merchMph: MintingPolicyHash = MintingPolicyHash::new(MERCHANT_MPH)
const merchAssetclass: AssetClass = AssetClass::new(
        merchMph, 
        "Merchant Token Littercoin".encode_utf8()
    )
const merchVal: Value = Value::new(merchAssetclass, 1)

func getDatum (txOuts : []TxOutput) -> []TxOutput {

    if (txOuts.is_empty()) {
        txOuts
    } else {
            txOuts.head.datum.switch {
            Inline => { []TxOutput::new_const(1, txOuts.head) },
            else => getDatum (txOuts.tail)
            }
    }
}

func main(datum: Datum, redeemer: Redeemer, ctx: ScriptContext) -> Bool {
    
    // Get hash of this validator
    vHash : ValidatorHash = ctx.get_current_validator_hash();
    tx : Tx = ctx.tx;
    txOutputs : []TxOutput = tx.outputs_locked_by(vHash);
    txOut : []TxOutput = getDatum(txOutputs);

    redeemer.switch {
        AddAda => {
            if (!txOut.is_empty()) {
                txOut.head.datum.switch {
                    dat: Inline => { 
                        outDat: Datum = Datum::from_data(dat.data);
                        addAdaDatumAmt: Int = outDat.adaAmount - datum.adaAmount;
                        adaVal: Value = Value::lovelace(outDat.adaAmount);

                        // Verify that the total Ada amount from the datum and
                        // the thread token is the same as the output value
                        // locked at the validator address                   
                        (tx.value_locked_by(vHash) == (ttVal + adaVal)).trace("LCV1: ") &&
                        (addAdaDatumAmt >= minAda).trace("LCV2: ")
                    },
                    else => false.trace("LCV3: ") // No inline datum found
                }
            } else {
                false.trace("LCV4: ") // No datum found in outputs
            }
        },
        Mint => {
             if (!txOut.is_empty()) {
                txOut.head.datum.switch {
                    dat: Inline => { 
                        outDat: Datum = Datum::from_data(dat.data);
                        adaDatumAmt: Int = outDat.adaAmount - datum.adaAmount;
                        adaVal: Value = Value::lovelace(outDat.adaAmount);
                        lcDatumAmt: Int = outDat.lcAmount - datum.lcAmount;
                        lcMintVal: Value = Value::new(lcAssetClass, lcDatumAmt);
            
                        // Verify that the total Ada amount from the datum and
                        // the thread token is the same as the output value
                        // locked at the validator address.  Also check the tx 
                        // is signed by the owner pkh.  

                        (adaDatumAmt == 0).trace("LCV5: ") && // Confirm no change in ada in datum
                        (tx.value_locked_by(vHash) == (ttVal + adaVal)).trace("LCV6: ") &&
                        (tx.minted == lcMintVal).trace("LCV7: ") &&
                        tx.is_signed_by(ownerPkh).trace("LCV8: ") &&  
                        (0 < lcDatumAmt && lcDatumAmt < outDat.adaAmount).trace("LCV9: ") 
                    },
                    else => false.trace("LCV10: " ) // Non inline datum found
                }
             } else {
                false.trace("LCV11: ") // No datum found in outputs
             }
        },
        red: Burn => {
             if (!txOut.is_empty()) {    
                txOut.head.datum.switch {
                    dat: Inline => { 
                        outDat: Datum = Datum::from_data(dat.data);
                        adaDatumAmt: Int = datum.adaAmount - outDat.adaAmount;
                        adaVal: Value = Value::lovelace(outDat.adaAmount);
                        lcDatumAmt: Int = datum.lcAmount - outDat.lcAmount;
                        lcBurnVal: Value = Value::new(lcAssetClass, lcDatumAmt) * (-1);
                        ratio: Int = datum.get_ratio();
                        adaWithdraw : Int = lcDatumAmt * ratio;
                        //adaWithdrawVal: Value = Value::lovelace(adaWithdraw);
                        merchPkh: PubKeyHash = PubKeyHash::new(red.pkhBA);
                        merchOutTxs : []TxOutput = tx.outputs_sent_to(merchPkh);
                        
                        // Verify that the amount of littercoin burned is the actual amount
                        // reduced by in the datum.  Also check that the Ada withdraw
                        // is equal to the amount of Ada remanining in the datum output.
                        // Finally confirm that thread token is sent to back to the validator
                        // with correct Ada amount                   
                        (outDat.adaAmount >= minAda).trace("LCV12: ") &&
                        (adaWithdraw >= minAda).trace("LCV13: ") &&
                        (adaDatumAmt == adaWithdraw).trace("LCV14: ") &&
                        (tx.minted == lcBurnVal).trace("LCV15: ") &&
                        //(merchOutTxs.head.value == (adaWithdrawVal + merchVal)).trace("LCV16: ") &&
                        (merchOutTxs.head.value == (minAdaVal + merchVal)).trace("LCV16: ") &&
                        (tx.value_locked_by(vHash) == (ttVal + adaVal)).trace("LCV17: ")
                    },
                    else => false.trace("LCV18: ") // No inline datum found
                }
            } else {
                false.trace("LCV19: ") // No datum found in outputs
            }
        }
    }
}

// Datum order is important
const LC_DATUM = Datum {
    adaAmount : 2000000,
    lcAmount : 0 
}

const VAL_ADD_ADA_REDEEMER = Redeemer::AddAda
const VAL_MINT_REDEEMER = Redeemer::Mint
const VAL_BURN_REDEEMER = Redeemer::Burn{#1a2b3c}

